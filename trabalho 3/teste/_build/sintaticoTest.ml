open Printf
open Lexing

open Ast
open ErroSint (*nome do modulo contendo as mensagens de erro*)

exception Erro_Sintatico of string

module  S = MenhirLib.General (*  Streams *)
module  I = Sintatico.MenhirInterpreter

let posicao lexbuf = 
      let pos = lexbuf.lex_curr_p in
      let lin = pos.pos_lnum
      and col = pos.pos_cnum - pos.pos_bol - 1 in
      sprintf "linha %d, coluna %d" lin col

(* [Pilha checkpoint] extrai a pilha do automato LR(1) contida em checkpoint *)

let pilha checkpoint = 
      match checkpoint with
        | I.HandlingError amb -> I.stack amb
        | _ ->  assert false (* Isso n'ao pode acontecer *)

let estado checkpoint : int = 
      match Lazy.force (pilha checkpoint) with
        | S.Nil -> (*O parser esta no estado inicial *)
          0
        | S.Cons (I.Element (s, _, _, _), _) ->
          I.number s

let sucesso v = Some v

let falha lexbuf (checkpoint : Ast.programa I.checkpoint) = 
  let estado_atual = estado checkpoint in
  let msg = message estado_atual in
  raise (Erro_Sintatico (Printf.sprintf "%d - %s.\n" (Lexing.lexeme_start lexbuf) msg))

let loop lexbuf resultado = 
  let fornecedor = I.lexer_lexbuf_to_supplier Lexico.token lexbuf in
  I.loop_handle sucesso (falha lexbuf) fornecedor resultado

let parse_com_erro lexbuf =
  try
    Some (loop lexbuf (Sintatico.Incremental.programa lexbuf.lex_curr_p))
  with
    | Lexico.Erro msg -> printf "Erro lexico na %s:\n\t%s\n" (posicao lexbuf) msg;
      None
    | Erro_Sintatico msg ->
      printf "Erro sintatico na %s %s\n" (posicao lexbuf) msg;
      None

let parse s =
  let lexbuf = Lexing.from_string s in
  let ast = parse_com_erro lexbuf in
  ast

let parse_arq nome = 
  let ic = open_in nome in
  let lexbuf = Lexing.from_channel ic in
  let result = parse_com_erro lexbuf in
  let _ = close_in ic in
  match result with
    | Some ast -> ast
    | None -> failwith "A analise sintatica falhou"

(* Para compilar:
  menhir -v --list-errors sintatico.mly > sintatico.msg
  menhir -v sintatico.mly --compile-errors sintatico.msg > erroSint.ml
  ocamlbuild -use-ocamlfind -use-menhir -menhir "menhir --table" -package menhirLib sintaticoTeste.byte
  *)